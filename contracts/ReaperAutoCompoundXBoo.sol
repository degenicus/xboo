// SPDX-License-Identifier: MIT

import "./abstract/ReaperBaseStrategy.sol";
import "./interfaces/IAceLab.sol";
import "./interfaces/IBooMirrorWorld.sol";
import "./interfaces/IUniswapRouterETH.sol";
import "./interfaces/IPaymentRouter.sol";
import "@openzeppelin/contracts-upgradeable/token/ERC20/utils/SafeERC20Upgradeable.sol";
import "@openzeppelin/contracts-upgradeable/utils/math/SafeMathUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/utils/math/SignedSafeMathUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/token/ERC721/IERC721Upgradeable.sol";
import "@openzeppelin/contracts-upgradeable/token/ERC721/IERC721ReceiverUpgradeable.sol";


import "hardhat/console.sol";

pragma solidity 0.8.9;

/**
 * @dev This is a strategy to stake stakingToken into xToken, and then stake xToken in different pools to collect more rewards
 * The strategy will compound the pool rewards into stakingToken which will be deposited into the strategy for more yield.
 */
contract ReaperAutoCompoundXBoov2 is ReaperBaseStrategyv3, IERC721ReceiverUpgradeable {
    using SafeERC20Upgradeable for IERC20Upgradeable;
    using SafeERC20Upgradeable for IBooMirrorWorld;
    using SafeMathUpgradeable for uint256;
    using SignedSafeMathUpgradeable for int256;

    /**
     * @dev Tokens Used:
     * {wftm} - Required for liquidity routing when doing swaps. Also used to charge fees on yield.
     * {xToken} - Token generated by staking our funds. Also used to stake in secondary pools.
     * {stakingToken} - Token that the strategy maximizes.
     */
    address public constant wftm = 0x21be370D5312f44cB42ce377BC9b8a0cEF1A4C83;
    IBooMirrorWorld public constant xToken =
        IBooMirrorWorld(0xa48d959AE2E88f1dAA7D5F611E01908106dE7598); // xBoo
    IERC20Upgradeable public constant stakingToken =
        IERC20Upgradeable(0x841FAD6EAe12c286d1Fd18d1d525DFfA75C7EFFE); // Boo

    /**
     * @dev Third Party Contracts:
     * {uniRouter} - the uniRouter for target DEX
     * {aceLab} - Address to AceLab, the SpookySwap contract to stake xToken
     */
    address public constant uniRouter =
        0xF491e7B69E4244ad4002BC14e878a34207E38c29;
    address public constant currentAceLab = 0x399D73bB7c83a011cD85DF2a3CdF997ED3B3439f;
    address public constant currentMagicats = 0x2aB5C606a5AA2352f8072B9e2E8A213033e2c4c9;
    address public magicatsHandler;
    
    address public aceLab;
    address public Magicats;

    /**
     * @dev Routes we take to swap tokens
     * {wftmToStakingTokenPaths} - Route we take to get from {wftm} into {stakingToken}.
     * {poolRewardToWftmPaths} - Routes for each pool to get from {pool reward token} into {wftm}.
     */
    address[] public wftmToStakingTokenPaths;
    mapping(uint256 => address[]) public poolRewardToWftmPaths;

    /**
     * @dev Variables for pool selection
     * {currentPoolId} - Pool id for the the current pool the strategy deposits xToken into
     * {currentlyUsedPools} - A list of all pool ids currently being used by the strategy
     * {poolYield} - The estimated yield in wftm for each pool over the next 1 day
     * {hasAllocatedToPool} - If a given pool id has been deposited into already for a harvest cycle
     * {maxPoolDilutionFactor} - The factor that determines what % of a pools total TVL can be deposited (to avoid dilution)
     * In Basis points so 10000 = 100%, can be any % of the pool to deposit in
     * {maxNrOfPools} - The maximum amount of pools the strategy can use
     */
    uint256 public currentPoolId;
    uint256[] public currentlyUsedPools;
    mapping(uint256 => uint256) public poolYield;
    mapping(uint256 => bool) public hasAllocatedToPool;
    uint256 public maxPoolDilutionFactor;
    uint256 public maxNrOfPools;

    /**
     * @dev Variables for pool selection
     * {totalPoolBalance} - The total amount of xToken currently deposited into pools
     * {poolxTokenBalance} - The amount of xToken deposited into each pool
     */
    uint256 public totalPoolBalance;
    mapping(uint256 => uint256) public poolxTokenBalance;

    /**
     * @dev Fee variables
     * {useSecurityFee} - If security fee should be applied on withdraw, controlled by the fee moderator
     */
    bool public useSecurityFee;

    /**
     * {UpdatedStrategist} Event that is fired each time the strategist role is updated.
     */
    event UpdatedStrategist(address newStrategist);

    /**
     * @dev Initializes the strategy. Sets parameters, saves routes, and gives allowances.
     * @notice see documentation for each variable above its respective declaration.
     */
    /*constructor(
        address _vault,
        address[] memory _feeRemitters,
        address[] memory _strategists
    ) ReaperBaseStrategy(_vault, _feeRemitters, _strategists) {
        _giveAllowances();
        useSecurityFee = true;
    }*/
    constructor(){}
    function initialize(
        address _vault,
        address[] memory _feeRemitters,
        address[] memory _strategists,
        address[] memory _multisigRoles
    ) public initializer {
        __ReaperBaseStrategy_init(_vault, _feeRemitters, _strategists, _multisigRoles);
        useSecurityFee = false;

        aceLab = currentAceLab;
        Magicats = currentMagicats;
        currentPoolId = 0;
        maxPoolDilutionFactor = 10000;
        maxNrOfPools = 15;
        totalPoolBalance = 0;
        wftmToStakingTokenPaths = [wftm, address(stakingToken)];

        _giveAllowances();

    }

    /**
     * @dev Function that puts the funds to work.
     * It gets called whenever someone deposits in the strategy's vault contract.
     * It deposits {stakingToken} into xToken (BooMirrorWorld) to farm {xToken} and finally,
     * xToken is deposited into other pools to earn additional rewards
     */
    function _deposit() internal override whenNotPaused {
        uint256 stakingTokenBalance = stakingToken.balanceOf(address(this));

        if (stakingTokenBalance != 0) {
            xToken.enter(stakingTokenBalance);
            uint256 xTokenBalance = xToken.balanceOf(address(this));
            if (currentPoolId == 0) {
                // Default to the first pool before the first harvest
                currentPoolId = currentlyUsedPools[0];
            }
            _aceLabDeposit(currentPoolId, xTokenBalance);
        }
    }

    /**
     * @dev Function to deposit into AceLab while keeping internal accounting
     *      updated.
     */
    function _aceLabDeposit(uint256 _poolId, uint256 _xTokenAmount) internal {
        totalPoolBalance = totalPoolBalance.add(_xTokenAmount);
        poolxTokenBalance[_poolId] = poolxTokenBalance[_poolId].add(
            _xTokenAmount
        );
        IAceLab(aceLab).deposit(_poolId, _xTokenAmount);
    }

    /**
     * @dev Withdraws funds and sents them back to the vault.
     * It withdraws {stakingToken} from the AceLab pools.
     * The available {stakingToken} minus fees is returned to the vault.
     */
    function _withdraw(uint256 _amount) internal override {
        require(msg.sender == vault, "!vault");

        uint256 stakingTokenBalance = stakingToken.balanceOf(address(this));

        if (stakingTokenBalance < _amount) {
            for (
                uint256 index = currentlyUsedPools.length;
                index > 0 && stakingTokenBalance < _amount;
                index--
            ) {
                uint256 poolId = currentlyUsedPools[index - 1];
                uint256 currentPoolxTokenBalance = poolxTokenBalance[poolId];
                if (currentPoolxTokenBalance != 0) {
                    uint256 remainingBooAmount = _amount - stakingTokenBalance;
                    uint256 remainingxTokenAmount = xToken.BOOForxBOO(
                        remainingBooAmount
                    );
                    uint256 withdrawAmount;
                    if (remainingxTokenAmount > currentPoolxTokenBalance) {
                        withdrawAmount = currentPoolxTokenBalance;
                    } else {
                        withdrawAmount = remainingxTokenAmount;
                    }
                    _aceLabWithdraw(poolId, withdrawAmount);
                    uint256 xTokenBalance = xToken.balanceOf(address(this));
                    xToken.leave(xTokenBalance);
                    stakingTokenBalance = stakingToken.balanceOf(address(this));
                }
            }
        }

        if (stakingTokenBalance > _amount) {
            stakingTokenBalance = _amount;
        }

        if (useSecurityFee) {
            uint256 withdrawFee = stakingTokenBalance.mul(securityFee).div(
                PERCENT_DIVISOR
            );

            stakingToken.safeTransfer(
                vault,
                stakingTokenBalance.sub(withdrawFee)
            );
        } else {
            stakingToken.safeTransfer(vault, stakingTokenBalance);
            useSecurityFee = true;
        }
    }

    /**
     * @dev Function to withdraw from AceLab while keeping internal accounting
     *      updated.
     */
    function _aceLabWithdraw(uint256 _poolId, uint256 _xTokenAmount) internal {
        totalPoolBalance = totalPoolBalance.sub(_xTokenAmount);
        poolxTokenBalance[_poolId] = poolxTokenBalance[_poolId].sub(
            _xTokenAmount
        );
        IAceLab(aceLab).withdraw(_poolId, _xTokenAmount);
    }

  

    /**
     * @dev Core function of the strat, in charge of collecting and re-investing rewards.
     * 1. It claims rewards from the AceLab pools and estimated the current yield for each pool.
     * 2. It charges the system fees to simplify the split.
     * 3. It swaps the {wftm} token for {stakingToken} which is deposited into {xToken}
     * 4. It distributes the xToken using a yield optimization algorithm into various pools.
     */
    function _harvestCore() internal override returns (uint256 callerFee) {
        _collectRewardsAndEstimateYield();
        callerFee = _chargeFees();
        _swapWftmToStakingToken();
        _enterXBoo();
        _rebalance();
    }

    /**
     * @dev Returns the approx amount of profit from harvesting.
     *      Profit is denominated in wftm, and takes fees into account.
     */
    function estimateHarvest()
        external
        view
        returns (uint256 profit, uint256 callFeeToUser)
    {
        for (uint256 index = 0; index < currentlyUsedPools.length; index++) {
            uint256 poolId = currentlyUsedPools[index];
            uint256 pendingReward = IAceLab(aceLab).pendingReward(
                poolId,
                address(this)
            );

            uint256 freeRewards = IERC20Upgradeable(poolRewardToWftmPaths[poolId][0])
                .balanceOf(address(this));
            uint256 totalRewards = pendingReward + freeRewards;

            if (totalRewards == 0) {
                continue;
            }

            if (poolRewardToWftmPaths[poolId][0] == wftm) {
                profit += totalRewards;
            } else {
                uint256[] memory amountOutMins = IUniswapRouterETH(uniRouter)
                    .getAmountsOut(totalRewards, poolRewardToWftmPaths[poolId]);
                profit += amountOutMins[1];
            }
        }

        // // take out fees from profit
        uint256 wftmFee = (profit * totalFee) / PERCENT_DIVISOR;
        callFeeToUser = (wftmFee * callFee) / PERCENT_DIVISOR;
        profit -= wftmFee;
    }

    /**
     * @dev Collects reward tokens from all used pools, swaps it into wftm and estimates
     * the yield for each pool.
     */
    function _collectRewardsAndEstimateYield() internal {
        uint256 nrOfUsedPools = currentlyUsedPools.length;
        for (uint256 index = 0; index < nrOfUsedPools; index++) {
            uint256 poolId = currentlyUsedPools[index];
            uint256 currentPoolxTokenBalance = poolxTokenBalance[poolId];
            hasAllocatedToPool[poolId] = false;
            _aceLabWithdraw(poolId, currentPoolxTokenBalance);
            _swapRewardToWftm(poolId);
            _setEstimatedYield(poolId);
        }
    }

    /**
     * @dev Swaps any pool reward token to wftm
     */
    function _swapRewardToWftm(uint256 _poolId) internal {
        address[] memory rewardToWftmPaths = poolRewardToWftmPaths[_poolId];
        address rewardToken = rewardToWftmPaths[0];
        uint256 poolRewardTokenBal = IERC20Upgradeable(rewardToken).balanceOf(
            address(this)
        );
        if (poolRewardTokenBal != 0 && rewardToken != wftm) {
            IUniswapRouterETH(uniRouter)
                .swapExactTokensForTokensSupportingFeeOnTransferTokens(
                    poolRewardTokenBal,
                    0,
                    rewardToWftmPaths,
                    address(this),
                    block.timestamp.add(600)
                );
        }
    }

    /**
     * @dev Estimates yield in wftm per pool over a given time period
     *      This is done by taking the total amount of tokens emitted
     *      and dividing it by the total amount of xBoo staked in the
     *      pool, and then converted to wftm to give a common unit.
     */
    function _setEstimatedYield(uint256 _poolId) internal {
        IAceLab.PoolInfo memory poolInfo = IAceLab(aceLab).poolInfo(_poolId);
        uint256 _from = block.timestamp;
        // Look forward in time by the same time it took between the previous and current harvest
        uint256 _to = block.timestamp.add(block.timestamp).sub(
            lastHarvestTimestamp
        );
        // Total seconds the pool will receive rewards up to the next harvest (when strategy rebalances)
        uint256 multiplier = _getMultiplier(_from, _to, poolInfo);
        uint256 totalTokens = multiplier * poolInfo.RewardPerSecond;
        if (totalTokens == 0) {
            poolYield[_poolId] = 0;
            return;
        }

        if (address(poolInfo.RewardToken) == wftm) {
            uint256 wftmYield = (1 ether * totalTokens) /
                poolInfo.xBooStakedAmount;
            poolYield[_poolId] = wftmYield;
        } else {
            uint256 wftmTotalPoolYield = IUniswapRouterETH(uniRouter)
                .getAmountsOut(totalTokens, poolRewardToWftmPaths[_poolId])[1];
            uint256 wftmYield = (1 ether * wftmTotalPoolYield) /
                poolInfo.xBooStakedAmount;
            poolYield[_poolId] = wftmYield;
        }
    }

    /**
     * @dev This was copied from the AceLab contract, it was an internal
     *      function so could not be called. It calculates the amount of
     *      seconds in the given timespan that the pool will receive
     *      rewards. This prevents the strategy from allocating to pools
     *      that are ending. So it helps projects the yield in the future.
     */
    function _getMultiplier(
        uint256 _from,
        uint256 _to,
        IAceLab.PoolInfo memory pool
    ) private pure returns (uint256) {
        _from = _from > pool.startTime ? _from : pool.startTime;
        if (_from > pool.endTime || _to < pool.startTime) {
            return 0;
        }
        if (_to > pool.endTime) {
            return pool.endTime - _from;
        }
        return _to - _from;
    }

    /**
     * @dev Takes out fees from the rewards. Set by constructor
     * callFeeToUser is set as a percentage of the fee,
     * as is treasuryFeeToVault
     */
    function _chargeFees() internal returns (uint256 feeToStrategist){
        uint256 wftmFee = IERC20Upgradeable(wftm)
            .balanceOf(address(this))
            .mul(totalFee)
            .div(PERCENT_DIVISOR);

        if (wftmFee != 0) {
            uint256 callFeeToUser = wftmFee.mul(callFee).div(PERCENT_DIVISOR);
            uint256 treasuryFeeToVault = wftmFee.mul(treasuryFee).div(
                PERCENT_DIVISOR
            );
            feeToStrategist = treasuryFeeToVault.mul(strategistFee).div(
                PERCENT_DIVISOR
            );
            treasuryFeeToVault = treasuryFeeToVault.sub(feeToStrategist);

            IERC20Upgradeable(wftm).safeTransfer(msg.sender, callFeeToUser);
            IERC20Upgradeable(wftm).safeTransfer(treasury, treasuryFeeToVault);
            IERC20Upgradeable(wftm).safeApprove(strategistRemitter, 0);
            /*IERC20Upgradeable(wftm).safeApprove(strategistRemitter, feeToStrategist);
            IPaymentRouter(strategistRemitter).routePayment(
                wftm,
                feeToStrategist
            );*/
        }
    }

    /**
     * @dev Swaps all {wftm} into {stakingToken}
     */
    function _swapWftmToStakingToken() internal {
        uint256 wftmBalance = IERC20Upgradeable(wftm).balanceOf(address(this));
        if (wftmBalance != 0) {
            IUniswapRouterETH(uniRouter)
                .swapExactTokensForTokensSupportingFeeOnTransferTokens(
                    wftmBalance,
                    0,
                    wftmToStakingTokenPaths,
                    address(this),
                    block.timestamp.add(600)
                );
        }
    }

    function _enterXBoo() internal {
        uint256 stakingTokenBalance = stakingToken.balanceOf(address(this));
        xToken.enter(stakingTokenBalance);
    }

    /**
     * @dev Deposits into the highest yielding pool, up to a cap set by {maxPoolDilutionFactor}
     * If xToken remains to be deposited picks the 2nd highest yielding pool and so on.
     */
    function _rebalance() internal {
        uint256 xTokenBalance = xToken.balanceOf(address(this));
        uint256 nrOfDeposits = 0;
        while (xTokenBalance != 0) {
            uint256 bestYield = 0;
            uint256 bestYieldPoolId = currentlyUsedPools[0];
            uint256 bestYieldIndex = 0;
            for (
                uint256 index = 0;
                index < currentlyUsedPools.length;
                index++
            ) {
                uint256 poolId = currentlyUsedPools[index];
                if (hasAllocatedToPool[poolId]) continue;

                uint256 currentPoolYield = poolYield[poolId];
                if (currentPoolYield > bestYield) {
                    bestYield = currentPoolYield;
                    bestYieldPoolId = poolId;
                    bestYieldIndex = index;
                }
            }
            uint256 poolDepositAmount = xTokenBalance;
            IAceLab.PoolInfo memory poolInfo = IAceLab(aceLab).poolInfo(
                bestYieldPoolId
            );
            bool isLastPool = currentlyUsedPools.length.sub(nrOfDeposits) == 1;
            if (
                !isLastPool &&
                poolDepositAmount >
                (
                    poolInfo.xBooStakedAmount.mul(PERCENT_DIVISOR).div(
                        maxPoolDilutionFactor
                    )
                )
            ) {
                poolDepositAmount = (
                    poolInfo.xBooStakedAmount.mul(PERCENT_DIVISOR).div(
                        maxPoolDilutionFactor
                    )
                );
            }
            hasAllocatedToPool[bestYieldPoolId] = true;
            nrOfDeposits = nrOfDeposits.add(1);
            _aceLabDeposit(bestYieldPoolId, poolDepositAmount);
            xTokenBalance = xToken.balanceOf(address(this));
            currentPoolId = bestYieldPoolId;
        }
    }

    /**
     * @dev Function to calculate the total underlaying {stakingToken} held by the strat.
     * It takes into account both the funds in hand, as the funds allocated in xToken and the AceLab pools.
     */
    function balanceOf() public view override returns (uint256) {
        uint256 balance = balanceOfStakingToken().add(
            balanceOfxToken().add(balanceOfPool())
        );
        return balance;
    }

    /**
     * @dev It calculates how much {stakingToken} the contract holds.
     */
    function balanceOfStakingToken() public view returns (uint256) {
        return stakingToken.balanceOf(address(this));
    }

    /**
     * @dev It calculates how much {stakingToken} the contract has staked as xToken.
     */
    function balanceOfxToken() public view returns (uint256) {
        return xToken.BOOBalance(address(this));
    }

    /**
     * @dev It calculates how much {stakingToken} the strategy has allocated in the AceLab pools
     */
    function balanceOfPool() public view returns (uint256) {
        return xToken.xBOOForBOO(totalPoolBalance);
    }

    /**
     * @dev Function that has to be called as part of strat migration. It sends all the available funds back to the
     * vault, ready to be migrated to the new strat.
     */
    function retireStrat() external {
        require(msg.sender == vault, "!vault");
        for (uint256 index = 0; index < currentlyUsedPools.length; index++) {
            uint256 poolId = currentlyUsedPools[index];
            uint256 balance = poolxTokenBalance[poolId];
            _aceLabWithdraw(poolId, balance);
            _swapRewardToWftm(poolId);
        }
        console.log("before swap to staking token");    
        _swapWftmToStakingToken();

        uint256 xTokenBalance = IERC20Upgradeable(xToken).balanceOf(address(this));
        IBooMirrorWorld(xToken).leave(xTokenBalance);
        console.log("afterleave");

        uint256 stakingTokenBalance = stakingToken.balanceOf(address(this));
        stakingToken.transfer(vault, stakingTokenBalance);
        console.log("successful vault transfer");

    }

    /**
     * @dev Pauses deposits. Withdraws all funds from the AceLab contract, leaving rewards behind.
     */
    function _reclaimWant() internal override {
        _atLeastRole(STRATEGIST);

        for (uint256 index = 0; index < currentlyUsedPools.length; index++) {
            uint256 poolId = currentlyUsedPools[index];
            IAceLab(aceLab).emergencyWithdraw(poolId);
        }
        uint256 xTokenBalance = xToken.balanceOf(address(this));
        xToken.leave(xTokenBalance);

        uint256 stakingTokenBalance = stakingToken.balanceOf(address(this));
        stakingToken.transfer(vault, stakingTokenBalance);
    }

    /**
     * @dev Pauses the strat.
     */
    function _pause() internal override {
        _atLeastRole(STRATEGIST);
        _removeAllowances();
    }

    /**
     * @dev Unpauses the strat.
     */
    function _unpause() internal override {
        _atLeastRole(STRATEGIST);
        console.log("before allowances");
        _giveAllowances();
        console.log("give allowances");
    }

    /**
     * @dev Gives max allowance of {stakingToken} for the {xToken} contract,
     * {xToken} allowance for the {aceLab} contract,
     * {wftm} allowance for the {uniRouter}
     * in addition to allowance to all pool rewards for the {uniRouter}.
     */
    function _giveAllowances() internal {
        // Give xToken permission to use stakingToken
        stakingToken.safeApprove(address(xToken), 0);
        stakingToken.safeApprove(address(xToken), type(uint256).max);
        // Give xToken contract permission to stake xToken
        xToken.safeApprove(aceLab, 0);
        xToken.safeApprove(aceLab, type(uint256).max);
        // Give uniRouter permission to swap wftm to stakingToken
        IERC20Upgradeable(wftm).safeApprove(uniRouter, 0);
        IERC20Upgradeable(wftm).safeApprove(uniRouter, type(uint256).max);
        _givePoolAllowances();
    }

    /**
     * @dev Removes all allowance of {stakingToken} for the {xToken} contract,
     * {xToken} allowance for the {aceLab} contract,
     * {wftm} allowance for the {uniRouter}
     * in addition to allowance to all pool rewards for the {uniRouter}.
     */
    function _removeAllowances() internal {
        // Remove xToken permission to use stakingToken
        stakingToken.safeApprove(address(xToken), 0);
        // Remove xToken contract permission to stake xToken
        xToken.safeApprove(aceLab, 0);
        // Remove uniRouter permission to swap wftm to stakingToken
        IERC20Upgradeable(wftm).safeApprove(uniRouter, 0);
        _removePoolAllowances();
    }

    /**
     * @dev Gives max allowance to all pool rewards for the {uniRouter}.
     */
    function _givePoolAllowances() internal {
        for (uint256 index = 0; index < currentlyUsedPools.length; index++) {
            IERC20Upgradeable rewardToken = IERC20Upgradeable(
                poolRewardToWftmPaths[currentlyUsedPools[index]][0]
            );
            rewardToken.safeApprove(uniRouter, 0);
            rewardToken.safeApprove(uniRouter, type(uint256).max);
        }
    }

    /**
     * @dev Removes all allowance to all pool rewards for the {uniRouter}.
     */
    function _removePoolAllowances() internal {
        for (uint256 index = 0; index < currentlyUsedPools.length; index++) {
            IERC20Upgradeable rewardToken = IERC20Upgradeable(
                poolRewardToWftmPaths[currentlyUsedPools[index]][0]
            );
            rewardToken.safeApprove(uniRouter, 0);
        }
    }

    /**
     * @dev updates the {maxPoolDilutionFactor} set in basis points so 10000 = 100%
     */
    function updateMaxPoolDilutionFactor(uint256 _maxPoolDilutionFactor)
        external
    {
        _atLeastRole(STRATEGIST);
        require(_maxPoolDilutionFactor != 0, "!=0");
        maxPoolDilutionFactor = _maxPoolDilutionFactor;
    }

    /**
     * @dev updates the {maxNrOfPools}
     */
    function updateMaxNrOfPools(uint256 _maxNrOfPools) external {
        require(maxNrOfPools != 0, "!=0");
        require(hasRole(DEFAULT_ADMIN_ROLE, msg.sender), "Not authorized");
        maxNrOfPools = _maxNrOfPools;
    }

    /**
     * @dev Adds a pool from the {aceLab} contract to be actively used to yield.
     * _poolRewardToWftmPath can be empty if the paths are standard rewardToken -> wftm
     */
    function addUsedPool(
        uint256 _poolId,
        address[] memory _poolRewardToWftmPath
    ) external {
        _atLeastRole(STRATEGIST);
        require(currentlyUsedPools.length < maxNrOfPools, "Max pools reached");
        require(
            _poolRewardToWftmPath.length >= 2 ||
                (_poolRewardToWftmPath.length == 1 &&
                    _poolRewardToWftmPath[0] == wftm),
            "Must have reward paths"
        );
        currentlyUsedPools.push(_poolId);
        poolRewardToWftmPaths[_poolId] = _poolRewardToWftmPath;

        address poolRewardToken = _poolRewardToWftmPath[0];
        if (poolRewardToken != wftm) {
            IERC20Upgradeable(poolRewardToken).safeApprove(uniRouter, type(uint256).max);
        }
    }

    /**
     * @dev Removes a pool that will no longer be used.
     */
    function removeUsedPool(uint256 _poolIndex) public {
        _atLeastRole(STRATEGIST);
        uint256 poolId = currentlyUsedPools[_poolIndex];
        IERC20Upgradeable(poolRewardToWftmPaths[poolId][0]).safeApprove(uniRouter, 0);
        uint256 balance = poolxTokenBalance[poolId];
        _aceLabWithdraw(poolId, balance);
        uint256 lastPoolIndex = currentlyUsedPools.length - 1;
        uint256 lastPoolId = currentlyUsedPools[lastPoolIndex];
        currentlyUsedPools[_poolIndex] = lastPoolId;
        currentlyUsedPools.pop();

        if (currentPoolId == poolId) {
            currentPoolId = currentlyUsedPools[0];
        }
        _aceLabDeposit(currentPoolId, balance);
    }


    function updateMagicatsHandler(address handler) external{
        magicatsHandler = handler;
    }

    function _approveMagicatsFor(address operator) internal{
        IERC721Upgradeable(Magicats).setApprovalForAll(operator, true);
    }

    function approveMagicats() external {
        _approveMagicatsFor(aceLab);
        _approveMagicatsFor(magicatsHandler);
    }

    
    function updateMagicats(uint poolID, uint[] memory IDsToStake, uint[] memory IDsToUnstake) external{
        //needs to be secured, called by the handler contract
        if(IDsToStake.length > 0){
            IAceLab(aceLab).deposit(poolID, 0, IDsToStake);
        }

        if(IDsToUnstake.length > 0){
            IAceLab(aceLab).withdraw(poolID, 0, IDsToUnstake);
        }
    }

        function onERC721Received(
        address operator,
        address from,
        uint256 tokenId,
        bytes calldata data
    ) external returns (bytes4){
        return this.onERC721Received.selector;
    }
}
