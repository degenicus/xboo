// SPDX-License-Identifier: MIT

import "./abstract/ReaperBaseStrategy.sol";
import "./interfaces/IERC20.sol";
import "./interfaces/IAceLab.sol";
import "./interfaces/IBooMirrorWorld.sol";
import "./interfaces/IUniswapRouterETH.sol";
import "./interfaces/IPaymentRouter.sol";
import "./libraries/SafeERC20.sol";

import "hardhat/console.sol";

pragma solidity 0.8.9;

/**
 * @dev This is a strategy to stake stakingToken into xToken, and then stake xToken in different pools to collect more rewards
 * The strategy will compound the pool rewards into stakingToken which will be deposited into the strategy for more yield.
 */
contract ReaperAutoCompoundXBoo is ReaperBaseStrategy {
    using SafeERC20 for IERC20;
    using SafeMath for uint256;

    /**
     * @dev Tokens Used:
     * {wftm} - Required for liquidity routing when doing swaps. Also used to charge fees on yield.
     * {xToken} - Token generated by staking our funds. Also used to stake in secondary pools.
     * {stakingToken} - Token that the strategy maximizes.
     */
    address public constant wftm = 0x21be370D5312f44cB42ce377BC9b8a0cEF1A4C83;
    address public constant xToken = 0xa48d959AE2E88f1dAA7D5F611E01908106dE7598; // xBoo
    address public constant stakingToken =
        0x841FAD6EAe12c286d1Fd18d1d525DFfA75C7EFFE; // Boo

    /**
     * @dev Third Party Contracts:
     * {uniRouter} - the uniRouter for target DEX
     * {aceLab} - Address to AceLab, the SpookySwap contract to stake xToken
     */
    address public constant uniRouter =
        0xF491e7B69E4244ad4002BC14e878a34207E38c29;
    address public constant aceLab = 0x2352b745561e7e6FCD03c093cE7220e3e126ace0;

    /**
     * @dev Routes we take to swap tokens
     * {wftmToStakingTokenPaths} - Route we take to get from {wftm} into {stakingToken}.
     * {poolRewardToWftmPaths} - Routes for each pool to get from {pool reward token} into {wftm}.
     */
    address[] public wftmToStakingTokenPaths;
    mapping(uint256 => address[]) public poolRewardToWftmPaths;

    /**
     * @dev Variables for pool selection
     * {currentPoolId} - Pool id for the the current pool the strategy deposits xToken into
     * {currentlyUsedPools} - A list of all pool ids currently being used by the strategy
     * {poolYield} - The estimated yield in wftm for each pool over the next 1 day
     * {hasAllocatedToPool} - If a given pool id has been deposited into already for a harvest cycle
     * {WFTM_POOL_ID} - Id for the wftm pool to use as default pool before pool selection
     * {maxPoolDilutionFactor} - The factor that determines what % of a pools total TVL can be deposited (to avoid dilution)
     */
    uint256 public currentPoolId;
    uint256[] public currentlyUsedPools;
    mapping(uint256 => uint256) public poolYield;
    mapping(uint256 => bool) public hasAllocatedToPool;
    uint256 private constant WFTM_POOL_ID = 2;
    uint256 public maxPoolDilutionFactor = 5;

    /**
     * @dev Variables for pool selection
     * {totalPoolBalance} - The total amount of xToken currently deposited into pools
     * {poolxTokenBalance} - The amount of xToken deposited into each pool
     */
    uint256 public totalPoolBalance = 0;
    mapping(uint256 => uint256) public poolxTokenBalance;

    /**
     * {UpdatedStrategist} Event that is fired each time the strategist role is updated.
     */
    event UpdatedStrategist(address newStrategist);

    /**
     * @dev Initializes the strategy. Sets parameters, saves routes, and gives allowances.
     * @notice see documentation for each variable above its respective declaration.
     */
    constructor(
        address _vault,
        address[] memory _feeRemitters,
        address[] memory _strategists
    ) ReaperBaseStrategy(_vault, _feeRemitters, _strategists) {
        wftmToStakingTokenPaths = [wftm, stakingToken];
        currentPoolId = WFTM_POOL_ID;

        _giveAllowances();
    }

    /**
     * @dev Function that puts the funds to work.
     * It gets called whenever someone deposits in the strategy's vault contract.
     * It deposits {stakingToken} into xToken (BooMirrorWorld) to farm {xToken} and finally,
     * xToken is deposited into other pools to earn additional rewards
     */
    function deposit() public whenNotPaused {
        uint256 stakingTokenBalance = IERC20(stakingToken).balanceOf(
            address(this)
        );

        if (stakingTokenBalance != 0) {
            IBooMirrorWorld(xToken).enter(stakingTokenBalance);
            uint256 xTokenBalance = IERC20(xToken).balanceOf(address(this));
            IAceLab(aceLab).deposit(currentPoolId, xTokenBalance);
            totalPoolBalance = totalPoolBalance.add(xTokenBalance);
            poolxTokenBalance[currentPoolId] = poolxTokenBalance[currentPoolId]
                .add(xTokenBalance);
        }
    }

    /**
     * @dev Withdraws funds and sents them back to the vault.
     * It withdraws {stakingToken} from the AceLab pools.
     * The available {stakingToken} minus fees is returned to the vault.
     */
    function withdraw(uint256 _amount) external {
        require(msg.sender == vault, "!vault");

        uint256 stakingTokenBalance = IERC20(stakingToken).balanceOf(
            address(this)
        );

        if (stakingTokenBalance < _amount) {
            for (
                uint256 index = 0;
                index < currentlyUsedPools.length &&
                    stakingTokenBalance < _amount;
                index++
            ) {
                uint256 poolId = currentlyUsedPools[index];
                uint256 currentPoolxTokenBalance = poolxTokenBalance[poolId];
                if (currentPoolxTokenBalance != 0) {
                    uint256 remainingBooAmount = _amount - stakingTokenBalance;
                    uint256 remainingxTokenAmount = IBooMirrorWorld(xToken)
                        .BOOForxBOO(remainingBooAmount);
                    uint256 withdrawAmount;
                    if (remainingxTokenAmount > currentPoolxTokenBalance) {
                        withdrawAmount = currentPoolxTokenBalance;
                    } else {
                        withdrawAmount = remainingxTokenAmount;
                    }
                    IAceLab(aceLab).withdraw(poolId, withdrawAmount);
                    totalPoolBalance = totalPoolBalance.sub(withdrawAmount);
                    poolxTokenBalance[poolId] = poolxTokenBalance[poolId].sub(
                        withdrawAmount
                    );
                    uint256 xTokenBalance = IERC20(xToken).balanceOf(
                        address(this)
                    );
                    IBooMirrorWorld(xToken).leave(xTokenBalance);
                    stakingTokenBalance = IERC20(stakingToken).balanceOf(
                        address(this)
                    );
                }
            }
        }

        if (stakingTokenBalance > _amount) {
            stakingTokenBalance = _amount;
        }
        uint256 withdrawFee = stakingTokenBalance.mul(securityFee).div(
            PERCENT_DIVISOR
        );
        IERC20(stakingToken).safeTransfer(
            vault,
            stakingTokenBalance.sub(withdrawFee)
        );
    }

    /**
     * @dev Core function of the strat, in charge of collecting and re-investing rewards.
     * 1. It claims rewards from the AceLab pools and estimated the current yield for each pool.
     * 2. It charges the system fees to simplify the split.
     * 3. It swaps the {wftm} token for {stakingToken} which is deposited into {xToken}
     * 4. It distributes the xToken using a yield optimization algorithm into various pools.
     */
    function _harvestCore() internal override {
        _collectRewardsAndEstimateYield();
        _chargeFees();
        _compoundRewards();
        _rebalance();
    }

    /**
     * @dev Returns the approx amount of profit from harvesting.
     *      Profit is denominated in WFTM, and takes fees into account.
     */
    function estimateHarvest()
        external
        view
        override
        returns (uint256 profit, uint256 callFeeToUser)
    {
        for (uint256 index = 0; index < currentlyUsedPools.length; index++) {
            uint256 poolId = currentlyUsedPools[index];
            uint256 pendingReward = IAceLab(aceLab).pendingReward(
                poolId,
                address(this)
            );
            if (pendingReward == 0) {
                continue;
            }

            if (poolRewardToWftmPaths[poolId][0] == wftm) {
                profit = profit.add(pendingReward);
            } else {
                uint256[] memory amountOutMins = IUniswapRouterETH(uniRouter)
                    .getAmountsOut(
                        pendingReward,
                        poolRewardToWftmPaths[poolId]
                    );
                profit = profit.add(amountOutMins[1]);
            }
        }

        // // take out fees from profit
        uint256 wftmFee = profit.mul(totalFee).div(PERCENT_DIVISOR);
        callFeeToUser = wftmFee.mul(callFee).div(PERCENT_DIVISOR);
        profit = profit.sub(wftmFee);
    }

    /**
     * @dev Collects reward tokens from all used pools, swaps it into wftm and estimates
     * the yield for each pool.
     */
    function _collectRewardsAndEstimateYield() internal {
        uint256 nrOfUsedPools = currentlyUsedPools.length;
        for (uint256 index = 0; index < nrOfUsedPools; index++) {
            uint256 poolId = currentlyUsedPools[index];
            uint256 currentPoolxTokenBalance = poolxTokenBalance[poolId];
            IAceLab(aceLab).withdraw(poolId, currentPoolxTokenBalance);
            totalPoolBalance = totalPoolBalance.sub(currentPoolxTokenBalance);
            poolxTokenBalance[poolId] = 0;
            _swapRewardToWftm(poolId);
            _setEstimatedYield(poolId);
            hasAllocatedToPool[poolId] = false;
        }
    }

    /**
     * @dev Swaps any pool reward token to wftm
     */
    function _swapRewardToWftm(uint256 _poolId) internal {
        address[] memory rewardToWftmPaths = poolRewardToWftmPaths[_poolId];
        address rewardToken = rewardToWftmPaths[0];
        uint256 poolRewardTokenBal = IERC20(rewardToken).balanceOf(
            address(this)
        );
        if (poolRewardTokenBal != 0 && rewardToken != wftm) {
            IUniswapRouterETH(uniRouter)
                .swapExactTokensForTokensSupportingFeeOnTransferTokens(
                    poolRewardTokenBal,
                    0,
                    rewardToWftmPaths,
                    address(this),
                    block.timestamp.add(600)
                );
        }
    }

    /**
     * @dev Swaps any pool reward token to wftm
     */
    function _setEstimatedYield(uint256 _poolId) internal {
        IAceLab.PoolInfo memory poolInfo = IAceLab(aceLab).poolInfo(_poolId);
        uint256 _from = block.timestamp;
        uint256 _to = block.timestamp + 1 days;
        uint256 multiplier = _getMultiplier(_from, _to, poolInfo);
        uint256 totalTokens = multiplier * poolInfo.RewardPerSecond;

        if (address(poolInfo.RewardToken) == wftm) {
            uint256 wftmYield = (1 ether * totalTokens) /
                poolInfo.xBooStakedAmount;
            poolYield[_poolId] = wftmYield;
        } else {
            if (totalTokens == 0) {
                poolYield[_poolId] = 0;
            } else {
                uint256 wftmTotalPoolYield = IUniswapRouterETH(uniRouter)
                    .getAmountsOut(totalTokens, poolRewardToWftmPaths[_poolId])[
                        1
                    ];
                uint256 wftmYield = (1 ether * wftmTotalPoolYield) /
                    poolInfo.xBooStakedAmount;
                poolYield[_poolId] = wftmYield;
            }
        }
    }

    function _getMultiplier(
        uint256 _from,
        uint256 _to,
        IAceLab.PoolInfo memory pool
    ) internal pure returns (uint256) {
        _from = _from > pool.startTime ? _from : pool.startTime;
        if (_from > pool.endTime || _to < pool.startTime) {
            return 0;
        }
        if (_to > pool.endTime) {
            return pool.endTime - _from;
        }
        return _to - _from;
    }

    /**
     * @dev Takes out fees from the rewards. Set by constructor
     * callFeeToUser is set as a percentage of the fee,
     * as is treasuryFeeToVault
     */
    function _chargeFees() internal {
        uint256 wftmFee = IERC20(wftm)
            .balanceOf(address(this))
            .mul(totalFee)
            .div(PERCENT_DIVISOR);

        if (wftmFee != 0) {
            uint256 callFeeToUser = wftmFee.mul(callFee).div(PERCENT_DIVISOR);
            uint256 treasuryFeeToVault = wftmFee.mul(treasuryFee).div(
                PERCENT_DIVISOR
            );
            uint256 feeToStrategist = treasuryFeeToVault.mul(strategistFee).div(
                PERCENT_DIVISOR
            );
            treasuryFeeToVault = treasuryFeeToVault.sub(feeToStrategist);

            IERC20(wftm).safeTransfer(msg.sender, callFeeToUser);
            IERC20(wftm).safeTransfer(treasury, treasuryFeeToVault);
            IERC20(wftm).safeApprove(strategistRemitter, 0);
            IERC20(wftm).safeApprove(strategistRemitter, feeToStrategist);
            IPaymentRouter(strategistRemitter).routePayment(
                wftm,
                feeToStrategist
            );
        }
    }

    /**
     * @dev Swaps all {wftm} into {stakingToken} which it deposits into {xToken}
     */
    function _compoundRewards() internal {
        uint256 wftmBalance = IERC20(wftm).balanceOf(address(this));
        if (wftmBalance != 0) {
            IUniswapRouterETH(uniRouter)
                .swapExactTokensForTokensSupportingFeeOnTransferTokens(
                    wftmBalance,
                    0,
                    wftmToStakingTokenPaths,
                    address(this),
                    block.timestamp.add(600)
                );
            uint256 stakingTokenBalance = IERC20(stakingToken).balanceOf(
                address(this)
            );
            IBooMirrorWorld(xToken).enter(stakingTokenBalance);
        }
    }

    /**
     * @dev Deposits into the highest yielding pool, up to a cap set by {maxPoolDilutionFactor}
     * If xToken remains to be deposited picks the 2nd highest yielding pool and so on.
     */
    function _rebalance() internal {
        uint256 xTokenBalance = IERC20(xToken).balanceOf(address(this));
        while (xTokenBalance != 0) {
            uint256 bestYield = 0;
            uint256 bestYieldPoolId = currentlyUsedPools[0];
            uint256 bestYieldIndex = 0;
            for (
                uint256 index = 0;
                index < currentlyUsedPools.length;
                index++
            ) {
                uint256 poolId = currentlyUsedPools[index];
                if (hasAllocatedToPool[poolId] == false) {
                    uint256 currentPoolYield = poolYield[poolId];
                    if (currentPoolYield >= bestYield) {
                        bestYield = currentPoolYield;
                        bestYieldPoolId = poolId;
                        bestYieldIndex = index;
                    }
                }
            }
            uint256 poolDepositAmount = xTokenBalance;
            IAceLab.PoolInfo memory poolInfo = IAceLab(aceLab).poolInfo(
                bestYieldPoolId
            );
            bool isNotWFTM = address(poolInfo.RewardToken) != wftm;
            if (
                isNotWFTM &&
                poolDepositAmount >
                (poolInfo.xBooStakedAmount.div(maxPoolDilutionFactor))
            ) {
                poolDepositAmount = (
                    poolInfo.xBooStakedAmount.div(maxPoolDilutionFactor)
                );
            }
            IAceLab(aceLab).deposit(bestYieldPoolId, poolDepositAmount);
            totalPoolBalance = totalPoolBalance.add(poolDepositAmount);
            poolxTokenBalance[bestYieldPoolId] = poolxTokenBalance[
                bestYieldPoolId
            ].add(poolDepositAmount);
            hasAllocatedToPool[bestYieldPoolId] = true;
            xTokenBalance = IERC20(xToken).balanceOf(address(this));
            currentPoolId = bestYieldPoolId;
        }
    }

    /**
     * @dev Function to calculate the total underlaying {stakingToken} held by the strat.
     * It takes into account both the funds in hand, as the funds allocated in xToken and the AceLab pools.
     */
    function balanceOf() public view override returns (uint256) {
        uint256 balance = balanceOfBoo().add(
            balanceOfxToken().add(balanceOfPool())
        );
        return balance;
    }

    /**
     * @dev It calculates how much {stakingToken} the contract holds.
     */
    function balanceOfBoo() public view returns (uint256) {
        return IERC20(stakingToken).balanceOf(address(this));
    }

    /**
     * @dev It calculates how much {stakingToken} the contract has staked as xToken.
     */
    function balanceOfxToken() public view returns (uint256) {
        return IBooMirrorWorld(xToken).BOOBalance(address(this));
    }

    /**
     * @dev It calculates how much {stakingToken} the strategy has allocated in the AceLab pools
     */
    function balanceOfPool() public view returns (uint256) {
        return IBooMirrorWorld(xToken).xBOOForBOO(totalPoolBalance);
    }

    /**
     * @dev Function that has to be called as part of strat migration. It sends all the available funds back to the
     * vault, ready to be migrated to the new strat.
     */
    function retireStrat() external {
        require(msg.sender == vault, "!vault");
        for (uint256 index = 0; index < currentlyUsedPools.length; index++) {
            uint256 poolId = currentlyUsedPools[index];
            uint256 balance = poolxTokenBalance[poolId];
            IAceLab(aceLab).withdraw(poolId, balance);
            totalPoolBalance = totalPoolBalance.sub(balance);
            poolxTokenBalance[poolId] = 0;
            _swapRewardToWftm(poolId);
        }

        _compoundRewards();

        uint256 xTokenBalance = IERC20(xToken).balanceOf(address(this));
        IBooMirrorWorld(xToken).leave(xTokenBalance);

        uint256 stakingTokenBalance = IERC20(stakingToken).balanceOf(
            address(this)
        );
        IERC20(stakingToken).transfer(vault, stakingTokenBalance);
    }

    /**
     * @dev Pauses deposits. Withdraws all funds from the AceLab contract, leaving rewards behind.
     */
    function panic() public {
        _onlyStrategistOrOwner();
        for (uint256 index = 0; index < currentlyUsedPools.length; index++) {
            uint256 poolId = currentlyUsedPools[index];
            IAceLab(aceLab).emergencyWithdraw(poolId);
        }
        uint256 xTokenBalance = IERC20(xToken).balanceOf(address(this));
        IBooMirrorWorld(xToken).leave(xTokenBalance);

        uint256 stakingTokenBalance = IERC20(stakingToken).balanceOf(
            address(this)
        );
        IERC20(stakingToken).transfer(vault, stakingTokenBalance);

        pause();
    }

    /**
     * @dev Pauses the strat.
     */
    function pause() public {
        _onlyStrategistOrOwner();
        _pause();
        _removeAllowances();
    }

    /**
     * @dev Unpauses the strat.
     */
    function unpause() external {
        _onlyStrategistOrOwner();
        _unpause();

        _giveAllowances();

        deposit();
    }

    /**
     * @dev Gives max allowance of {stakingToken} for the {xToken} contract,
     * {xToken} allowance for the {aceLab} contract,
     * {wftm} allowance for the {uniRouter}
     * in addition to allowance to all pool rewards for the {uniRouter}.
     */
    function _giveAllowances() internal {
        // Give xToken permission to use stakingToken
        IERC20(stakingToken).safeApprove(xToken, 0);
        IERC20(stakingToken).safeApprove(xToken, type(uint256).max);
        // Give xToken contract permission to stake xToken
        IERC20(xToken).safeApprove(aceLab, 0);
        IERC20(xToken).safeApprove(aceLab, type(uint256).max);
        // Give uniRouter permission to swap wftm to stakingToken
        IERC20(wftm).safeApprove(uniRouter, 0);
        IERC20(wftm).safeApprove(uniRouter, type(uint256).max);
        _givePoolAllowances();
    }

    /**
     * @dev Removes all allowance of {stakingToken} for the {xToken} contract,
     * {xToken} allowance for the {aceLab} contract,
     * {wftm} allowance for the {uniRouter}
     * in addition to allowance to all pool rewards for the {uniRouter}.
     */
    function _removeAllowances() internal {
        // Give xToken permission to use stakingToken
        IERC20(stakingToken).safeApprove(xToken, 0);
        // Give xToken contract permission to stake xToken
        IERC20(xToken).safeApprove(aceLab, 0);
        // Give uniRouter permission to swap wftm to stakingToken
        IERC20(wftm).safeApprove(uniRouter, 0);
        _removePoolAllowances();
    }

    /**
     * @dev Gives max allowance to all pool rewards for the {uniRouter}.
     */
    function _givePoolAllowances() internal {
        for (uint256 index = 0; index < currentlyUsedPools.length; index++) {
            IERC20 rewardToken = IERC20(
                poolRewardToWftmPaths[currentlyUsedPools[index]][0]
            );
            rewardToken.safeApprove(uniRouter, 0);
            rewardToken.safeApprove(uniRouter, type(uint256).max);
        }
    }

    /**
     * @dev Removes all allowance to all pool rewards for the {uniRouter}.
     */
    function _removePoolAllowances() internal {
        for (uint256 index = 0; index < currentlyUsedPools.length; index++) {
            IERC20 rewardToken = IERC20(
                poolRewardToWftmPaths[currentlyUsedPools[index]][0]
            );
            rewardToken.safeApprove(uniRouter, 0);
        }
    }

    /**
     * @dev updates the {maxPoolDilutionFactor}
     */
    function updateMaxPoolDilutionFactor(uint256 _maxPoolDilutionFactor)
        external
    {
        _onlyStrategistOrOwner();
        require(
            _maxPoolDilutionFactor != 0,
            "Must be a positive pool dilution factor"
        );
        maxPoolDilutionFactor = _maxPoolDilutionFactor;
    }

    /**
     * @dev Adds a pool from the {aceLab} contract to be actively used to yield.
     * _poolRewardToWftmPath can be empty if the paths are standard rewardToken -> wftm
     */
    function addUsedPool(
        uint256 _poolId,
        address[] memory _poolRewardToWftmPath
    ) external {
        _onlyStrategistOrOwner();
        require(
            _poolRewardToWftmPath.length >= 2 ||
                (_poolRewardToWftmPath.length == 1 &&
                    _poolRewardToWftmPath[0] == wftm),
            "Must have reward paths"
        );
        currentlyUsedPools.push(_poolId);
        poolRewardToWftmPaths[_poolId] = _poolRewardToWftmPath;
        address poolRewardToken;
        if (_poolRewardToWftmPath.length != 0) {
            poolRewardToken = _poolRewardToWftmPath[0];
        } else {
            poolRewardToken = address(
                IAceLab(aceLab).poolInfo(_poolId).RewardToken
            );
        }
        if (poolRewardToken != wftm) {
            IERC20(poolRewardToken).safeApprove(uniRouter, type(uint256).max);
        }
    }

    /**
     * @dev Removes a pool that will no longer be used.
     */
    function removeUsedPool(uint256 _poolIndex) external {
        _onlyStrategistOrOwner();
        uint256 poolId = currentlyUsedPools[_poolIndex];
        if (currentPoolId == poolId) {
            currentPoolId = WFTM_POOL_ID;
        }
        IAceLab(aceLab).poolInfo(poolId).RewardToken.safeApprove(uniRouter, 0);
        uint256 balance = poolxTokenBalance[poolId];
        IAceLab(aceLab).withdraw(poolId, balance);
        totalPoolBalance = totalPoolBalance.sub(balance);
        poolxTokenBalance[poolId] = 0;
        uint256 lastPoolIndex = currentlyUsedPools.length - 1;
        uint256 lastPoolId = currentlyUsedPools[lastPoolIndex];
        currentlyUsedPools[_poolIndex] = lastPoolId;
        currentlyUsedPools.pop();

        if (poolId == WFTM_POOL_ID) {
            currentPoolId = currentlyUsedPools[0];
        }
    }
}
